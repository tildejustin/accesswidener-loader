plugins {
    alias libs.plugins.fabric.loom
    id "maven-publish"
}

version = mod_version
group = maven_group

base {
    archivesName = archives_name
}

repositories {
    maven { url "https://jitpack.io" }
}

loom {
    runConfigs.configureEach {
        ideConfigGenerated = false
    }
}

dependencies {
    minecraft libs.minecraft
    mappings variantOf(libs.yarn.mappings) { classifier "" }
    modImplementation libs.fabric.loader
    vineflowerDecompilerClasspath libs.vineflower
    include(implementation "net.fabricmc:mapping-io:0.5.1")
}

processResources {
    filesMatching("fabric.mod.json") {
        expand "version": version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    if (JavaVersion.current().isJava9Compatible()) it.options.release.set(8)
}

java {
    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8
}

jar {
    from("LICENSE")
}

publishing {
    publications.create("mavenJava", MavenPublication) {
        from components.java
    }
    repositories {}
}
